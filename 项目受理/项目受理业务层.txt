*** Settings ***
Resource          项目受理操作层.txt
Resource          项目受理校验层.txt
Resource          ../询价/组件层/住宅信息.txt

*** Keywords ***
提取任务进行立项操作
    ${流水号}    根据查询条件随机获取一个项目流水号
    ${pep}    查询项目详情并获取一个pep对象    ${流水号}
    打开功能模块    询价立项管理    项目受理
    Select Frame    id=InquiryAndProject_ProjectAccept
    点击立项按钮进入立项界面
    ${所属部门}    Get Value    xpath=.//*[@id='Department']
    ${所属部门}    Get Text    xpath=.//*[@id='Department']/option[@value='${所属部门}']
    进行提取信息操作    ${pep.get_value(u'流水号')}
    Set Pep Value    ${pep}    所属部门    ${所属部门}
    验证立项界面的值是否正常    ${pep}
    ${pep_new}    Create Pep Object
    将立项界面的信息写入pep对象    ${pep_new}
    进行立项操作    ${pep_new}
    查询并验证项目详情的值是否正确    ${pep_new}

新项目立项操作
    [Arguments]    ${input_pep}
    [Documentation]    新项目立项操作，只需要传入一个初始化的pep对象，该方法会将该对象里的值填写到立项界面上，改方法使用各种数据录入，最后会返回一个新的pep对象
    打开功能模块    询价立项管理    项目受理
    Transaction Start    打开项目受理页面
    Select Frame    id=InquiryAndProject_ProjectAccept
    等待列表创建完毕
    Transaction End    打开项目受理页面
    切换到暂存项目子模块
    等待列表创建完毕    打开暂存项目模块
    点击立项按钮进入立项界面
    填写立项界面的信息    ${input_pep}
    ${pep}    Create Pep Object    #将界面上的信息写入一个新的pep对象
    将立项界面的信息写入pep对象    ${pep}
    进行立项操作    ${pep}
    [Return]    ${pep}

暂存后进行任务分配操作
    [Arguments]    ${input_pep}
    [Documentation]    先做暂存操作，再做立项操作
    ${pep}    暂存并查询结果流程    ${input_pep}
    点击列表的编辑按钮进入审核项目
    验证立项界面的值是否正常    ${pep}
    进行任务分配操作    ${pep}
    [Return]    ${pep}

暂存后进行立项操作
    [Arguments]    ${input_pep}
    [Documentation]    先做暂存操作，再做立项操作
    ${pep}    暂存并查询结果流程    ${input_pep}
    点击列表的编辑按钮进入审核项目
    验证立项界面的值是否正常    ${pep}
    进行立项操作    ${pep}
    查询并验证项目详情的值是否正确    ${pep}
    [Return]    ${pep}

暂存并查询结果流程
    [Arguments]    ${input_pep}
    [Documentation]    做暂存项目操作，并通过暂存项目列表查询验证
    打开功能模块    询价立项管理    项目受理
    Select Frame    id=InquiryAndProject_ProjectAccept
    等待列表创建完毕
    切换到暂存项目子模块
    等待列表创建完毕    打开暂存项目模块
    点击立项按钮进入立项界面
    通过手机号码获取客户信息    ${input_pep}
    填写项目信息    ${input_pep}
    填写收费信息    ${input_pep}
    填写勘查信息    ${input_pep}
    填写收件信息    ${input_pep}
    填写备注信息    ${input_pep.get_value(u'立项备注')}
    ${pep}    Create Pep Object
    将立项界面的信息写入pep对象    ${pep}
    进行暂存操作    ${pep}
    Select Frame    id=InquiryAndProject_ProjectAccept
    切换到暂存项目子模块
    输入暂存项目查询条件    ${pep.get_value(u'流水号')}
    点击查询按钮
    等待列表创建完毕    暂存项目查询
    验证暂存项目查询数据    ${pep.get_value(u'流水号')}    ${pep.get_value(u'项目地址')}    ${pep.get_value(u'小区名')}    ${pep.get_value(u'项目分类')}    ${pep.get_value(u'物业类型')}    ${pep.get_value(u'估价目的')}
    ...    ${pep.get_value(u'报告类型')}    ${pep.get_value(u'业务来源')}    ${pep.get_value(u'客户姓名')}    ${pep.get_value(u'估价委托方')}
    [Return]    ${pep}

查询并修改项目信息操作
    [Arguments]    ${流水号}    ${pep}    # 传入需要修改的流水号
    打开功能模块    询价立项管理    项目受理
    Select Frame    id=InquiryAndProject_ProjectAccept
    切换到已立项子模块
    输入已立项查询条件    ${流水号}
    点击查询按钮
    等待列表创建完毕    已立项查询
    点击列表中的编辑按钮
    Unselect Frame
    Select Frame    xpath=//iframe[contains(@id,"create_project_")]
    等待加载完毕
    填写立项界面的信息    ${pep}
    ${pep_new}    Create Pep Object
    将立项界面的信息写入pep对象    ${pep_new}
    Set Pep Value    ${pep_new}    流水号    ${流水号}
    点击立项页面中的保存按钮
    等待提示框出现
    点击提示框中的确定按钮
    Unselect Frame
    [Return]    ${pep_new}

查询并验证项目详情的值是否正确
    [Arguments]    ${pep}
    打开功能模块    询价立项管理    项目受理
    Select Frame    id=InquiryAndProject_ProjectAccept
    切换到已立项子模块
    等待列表创建完毕
    ${查询_流水号}    Set Variable If    '${pep.get_value(u'旧流水号')}'!=''    ${EMPTY}    ${pep.get_value(u'流水号')}
    输入已立项查询条件    ${查询_流水号}    ${pep.get_value(u'报告号')}
    点击查询按钮
    #等待加载完毕
    等待列表创建完毕
    ${流水号}    Get Text    xpath=.//div[@class="panel" and contains(@style,"display: block")]//*[@class="datagrid-btable"]//tr[@datagrid-row-index="0"]/td[@field="ProjectNo"]/div
    Run Keyword If    '${pep.get_value(u'旧流水号')}'!=''    Should Not Be Equal    ${流水号}    ${pep.get_value(u'旧流水号')}
    Run Keyword If    '${pep.get_value(u'旧流水号')}'!=''    Set Pep Value    ${pep}    流水号    ${流水号}
    Comment    验证已立项查询数据    流水号=${查询_流水号}    报告号=${pep.get_value(u'报告号')}    旧流水号=${pep.get_value(u'旧流水号')}
    点击列表中的查看按钮
    Unselect Frame
    Wait Until Page Contains Element    xpath=//iframe[contains(@id,'${pep.get_value(u'流水号')}')]
    Select Frame    xpath=//iframe[contains(@id,'${pep.get_value(u'流水号')}')]
    ${是否项目变更}    Set Variable If    '${pep.get_value(u'旧流水号')}'!=''    4    0
    验证立项界面的值是否正常    ${pep}    1    ${是否项目变更}
    Unselect Frame
    关闭功能模块    项目详细-${pep.get_value(u'流水号')}

根据查询条件随机获取一个项目流水号
    [Arguments]    ${流水号}=None    ${报告编号}=None    ${项目地址}=None    ${小区名称}=None    ${估价目的}=None    ${项目类型}=None
    ...    ${业务来源}=None    ${物业类型}=None    ${受理人}=None    ${受理开始时间}=None    ${受理结束时间}=None    ${估价委托方}=None
    ...    ${客户姓名}=None    ${客户手机}=None    ${看房联系人电话}=None    ${资料是否齐全}=None    ${收费责任人}=None    ${外业是否完成}=None
    打开功能模块    询价立项管理    项目受理
    Select Frame    id=InquiryAndProject_ProjectAccept
    等待列表创建完毕
    切换到已立项子模块
    等待列表创建完毕
    输入已立项查询条件    ${流水号}    ${报告编号}    ${项目地址}    ${小区名称}    ${估价目的}    ${项目类型}
    ...    ${业务来源}    ${物业类型}    ${受理人}    ${受理开始时间}    ${受理结束时间}    ${估价委托方}
    ...    ${客户姓名}    ${客户手机}    ${看房联系人电话}    ${资料是否齐全}    ${收费责任人}    ${外业是否完成}
    点击查询按钮
    等待列表创建完毕
    ${page_count_msg}    Get Text    xpath=.//div[@class="panel" and contains(@style,"display: block")]//div[contains(@class,"datagrid-pager")]//td[8]/span    #获取列表总页数
    ${page_count}    Findall    共(.*)页    ${page_count_msg}    #获取列表总页数
    ${page}    Evaluate    random.randint(1,${page_count[0]})    modules=random    #获取一个随机页数
    Input Text    xpath=.//div[@class="panel" and contains(@style,"display: block")]//div[contains(@class,"datagrid-pager")]//input[@class="pagination-num"]    ${page}
    Press Key    xpath=.//div[@class="panel" and contains(@style,"display: block")]//div[contains(@class,"datagrid-pager")]//input[@class="pagination-num"]    \\13
    等待列表创建完毕
    ${row_count}    Get Matching Xpath Count    .//div[@class="panel" and contains(@style,"display: block")]//*[@class="datagrid-btable"]//td[@field="ProjectNo"]/..    #获取当前页的总行数
    ${row}    Evaluate    random.randint(1,${row_count})    modules=random    #获取一个随机页数
    ${流水号}    Get Text    xpath=.//div[@class="panel" and contains(@style,"display: block")]//*[@class="datagrid-btable"]//tr[${row}]/td[@field="ProjectNo"]/div
    Unselect Frame
    [Return]    ${流水号}

立项并继续操作
    [Arguments]    @{pep_object_list}    # pep对象列表
    打开功能模块    询价立项管理    项目受理
    Select Frame    id=InquiryAndProject_ProjectAccept
    等待列表创建完毕
    切换到暂存项目子模块
    等待列表创建完毕    打开暂存项目模块
    点击立项按钮进入立项界面
    ${pep_list}    Create List
    : FOR    ${input_pep}    IN    @{pep_object_list}
    \    通过手机号码获取客户信息    ${input_pep}
    \    填写项目信息    ${input_pep}
    \    填写收费信息    ${input_pep}
    \    填写勘查信息    ${input_pep}
    \    填写收件信息    ${input_pep}
    \    填写备注信息    ${input_pep.get_value(u'立项备注')}
    \    ${pep}    Create Pep Object
    \    将立项界面的信息写入pep对象    ${pep}
    \    进行立项并继续操作    ${pep}
    \    Append To List    ${pep_list}    ${pep}
    Unselect Frame
    : FOR    ${pep}    IN    @{pep_list}
    \    查询并验证项目详情的值是否正确    ${pep}

立项时直接任务分配操作
    [Arguments]    ${input_pep}
    打开功能模块    询价立项管理    项目受理
    Select Frame    id=InquiryAndProject_ProjectAccept
    等待列表创建完毕
    切换到暂存项目子模块
    等待列表创建完毕    打开暂存项目模块
    点击立项按钮进入立项界面
    通过手机号码获取客户信息    ${input_pep}
    填写项目信息    ${input_pep}
    填写收费信息    ${input_pep}
    填写勘查信息    ${input_pep}
    填写收件信息    ${input_pep}
    填写备注信息    ${input_pep.get_value(u'立项备注')}
    ${pep}    Create Pep Object
    将立项界面的信息写入pep对象    ${pep}
    进行任务分配操作    ${pep}
    [Return]    ${pep}

进行询价操作
    [Arguments]    ${pep}
    Click Element    xpath=.//span[text()="询价"]/ancestor::a    #点击立项界面上的询价按钮
    Unselect Frame
    select frame    id=add_inquiry_house
    等待加载完毕
    获取添加询价所有数据校验    ${pep}
    询价按钮
    获取询价系统价格    ${pep}
    Unselect Frame

重复暂存操作
    [Arguments]    ${input_pep}
    [Documentation]    做两次暂存操作，并返回一个pep对象
    ${pep}    暂存并查询结果流程    ${input_pep}
    点击列表的编辑按钮进入审核项目
    验证立项界面的值是否正常    ${pep}
    进行暂存操作    ${pep}
    Select Frame    id=InquiryAndProject_ProjectAccept
    切换到暂存项目子模块
    输入暂存项目查询条件    ${pep.get_value(u'流水号')}
    点击查询按钮
    等待列表创建完毕
    验证暂存项目查询数据    ${pep.get_value(u'流水号')}    ${pep.get_value(u'项目地址')}    ${pep.get_value(u'小区名')}    ${pep.get_value(u'项目分类')}    ${pep.get_value(u'物业类型')}    ${pep.get_value(u'估价目的')}
    ...    ${pep.get_value(u'报告类型')}    ${pep.get_value(u'业务来源')}    ${pep.get_value(u'客户姓名')}    ${pep.get_value(u'估价委托方')}
    [Return]    ${pep}

线上业务有效查询
    [Arguments]    ${交易编号}=None    ${项目流水号}=None    ${小区名称}=None    ${物业地址}=None    ${项目来源}=None    ${是否已受理}=None
    ...    ${是否已终止}=None    ${业务类型}=None
    打开功能模块    询价立项管理    项目受理
    Select Frame    id=InquiryAndProject_ProjectAccept
    等待列表创建完毕
    切换到在线业务子模块
    等待列表创建完毕
    输入线上业务查询条件    ${交易编号}    ${项目流水号}    ${小区名称}    ${物业地址}    ${项目来源}    ${是否已受理}
    ...    ${是否已终止}    ${业务类型}
    点击查询按钮
    等待列表创建完毕    线上业务查询
    ${模糊查询关键字}    Set Variable If    "${交易编号}"!="None"    ${交易编号}    "${项目流水号}"!="None"    ${项目流水号}    "${小区名称}"!="None"
    ...    ${小区名称}    "${物业地址}"!="None"    ${物业地址}    ${EMPTY}
    Run Keyword If    "${模糊查询关键字}"!="None"    验证模糊查询后的列表数据    ${模糊查询关键字}
    验证线上业务查询数据    是否受理=${是否已受理}    项目来源=${项目来源}    是否终止=${是否已终止}    业务类型=${业务类型}
    Comment    清空线上业务查询条件
    Unselect Frame

线上业务无效查询
    [Arguments]    ${交易编号}=None    ${项目流水号}=None    ${小区名称}=None    ${物业地址}=None    ${项目来源}=None    ${是否已受理}=None
    ...    ${是否已终止}=None    ${业务类型}=None
    打开功能模块    询价立项管理    项目受理
    Select Frame    id=InquiryAndProject_ProjectAccept
    等待列表创建完毕
    切换到在线业务子模块
    等待列表创建完毕
    输入线上业务查询条件    ${交易编号}    ${项目流水号}    ${小区名称}    ${物业地址}    ${项目来源}    ${是否已受理}
    ...    ${是否已终止}    ${业务类型}
    点击查询按钮
    等待列表创建完毕
    ${表格列数}    Get Matching Xpath Count    .//div[@class="panel" and contains(@style,"display: block")]//*[@class="datagrid-btable"]//tr[@datagrid-row-index]/td[@field="bscode"]/div
    Run Keyword And Continue On Failure    Should Be Equal    ${表格列数}    0
    Comment    清空线上业务查询条件
    Unselect Frame

暂存项目有效查询
    [Arguments]    ${流水号}=None    ${项目地址}=None    ${小区名称}=None    ${估价委托方}=None    ${项目分类}=None    ${物业类型}=None
    ...    ${估价目的}=None    ${业务来源}=None    ${开始时间}=None    ${结束时间}=None
    打开功能模块    询价立项管理    项目受理
    Select Frame    id=InquiryAndProject_ProjectAccept
    等待列表创建完毕
    切换到暂存项目子模块
    等待列表创建完毕    打开暂存项目模块
    输入暂存项目查询条件    ${流水号}    ${项目地址}    ${小区名称}    ${估价委托方}    ${项目分类}    ${物业类型}
    ...    ${估价目的}    ${业务来源}    ${开始时间}    ${结束时间}
    点击查询按钮
    等待列表创建完毕    暂存项目查询
    ${模糊查询关键字}    Set Variable If    "${流水号}"!="None"    ${流水号}    "${项目地址}"!="None"    ${项目地址}    "${小区名称}"!="None"
    ...    ${小区名称}    "${估价委托方}"!="None"    ${估价委托方}    ${EMPTY}
    Run Keyword If    "${模糊查询关键字}"!="None"    验证模糊查询后的列表数据    ${模糊查询关键字}
    验证暂存项目查询数据    项目分类=${项目分类}    物业类型=${物业类型}    估价目的=${估价目的}    业务来源=${业务来源}
    Comment    清空暂存项目查询条件
    Unselect Frame

暂存项目无效查询
    [Arguments]    ${流水号}=None    ${项目地址}=None    ${小区名称}=None    ${估价委托方}=None    ${项目分类}=None    ${物业类型}=None
    ...    ${估价目的}=None    ${业务来源}=None    ${开始时间}=None    ${结束时间}=None
    打开功能模块    询价立项管理    项目受理
    Select Frame    id=InquiryAndProject_ProjectAccept
    等待列表创建完毕
    切换到暂存项目子模块
    等待列表创建完毕    打开暂存项目模块
    输入暂存项目查询条件    ${流水号}    ${项目地址}    ${小区名称}    ${估价委托方}    ${项目分类}    ${物业类型}
    ...    ${估价目的}    ${业务来源}    ${开始时间}    ${结束时间}
    点击查询按钮
    等待列表创建完毕
    ${表格列数}    Get Matching Xpath Count    .//div[@class="panel" and contains(@style,"display: block")]//*[@class="datagrid-btable"]//tr[@datagrid-row-index]/td[@field="bscode"]/div
    Run Keyword And Continue On Failure    Should Be Equal    ${表格列数}    0
    Comment    清空暂存项目查询条件
    Unselect Frame

已立项有效查询
    [Arguments]    ${流水号}=None    ${报告编号}=None    ${项目地址}=None    ${小区名称}=None    ${估价目的}=None    ${报告类型}=None
    ...    ${业务来源}=None    ${物业类型}=None    ${受理人}=None    ${受理开始时间}=None    ${受理结束时间}=None    ${估价委托方}=None
    ...    ${客户姓名}=None    ${客户手机}=None    ${看房联系人电话}=None    ${资料是否齐全}=None    ${收费责任人}=None    ${外业是否完成}=None
    打开功能模块    询价立项管理    项目受理
    Transaction Start    打开项目受理页面
    Select Frame    id=InquiryAndProject_ProjectAccept
    等待列表创建完毕
    Transaction End    打开项目受理页面
    切换到已立项子模块
    等待列表创建完毕    打开已立项界面
    输入已立项查询条件    ${流水号}    ${报告编号}    ${项目地址}    ${小区名称}    ${估价目的}    ${报告类型}
    ...    ${业务来源}    ${物业类型}    ${受理人}    ${受理开始时间}    ${受理结束时间}    ${估价委托方}
    ...    ${客户姓名}    ${客户手机}    ${看房联系人电话}    ${资料是否齐全}    ${收费责任人}    ${外业是否完成}
    点击查询按钮
    等待列表创建完毕    已立项-查询
    ${模糊查询关键字}    Set Variable If    "${流水号}"!="None"    ${流水号}    "${报告编号}"!="None"    ${报告编号}    "${项目地址}"!="None"
    ...    ${项目地址}    "${小区名称}"!="None"    ${小区名称}    ${EMPTY}
    Run Keyword If    "${模糊查询关键字}"!="None"    验证模糊查询后的列表数据    ${模糊查询关键字}
    验证已立项查询数据    报告类型=${报告类型}    物业类型=${物业类型}    估价目的=${估价目的}    资料齐全=${资料是否齐全}    客户姓名=${客户姓名}    受理人=${受理人}
    ...    估价委托方=${估价委托方}    业务来源=${业务来源}
    Comment    清空已立项查询条件
    Unselect Frame

已立项无效查询
    [Arguments]    ${流水号}=None    ${报告编号}=None    ${项目地址}=None    ${小区名称}=None    ${估价目的}=None    ${报告类型}=None
    ...    ${业务来源}=None    ${物业类型}=None    ${受理人}=None    ${受理开始时间}=None    ${受理结束时间}=None    ${估价委托方}=None
    ...    ${客户姓名}=None    ${客户手机}=None    ${看房联系人电话}=None    ${资料是否齐全}=None    ${收费责任人}=None    ${外业是否完成}=None
    打开功能模块    询价立项管理    项目受理
    Transaction Start    打开项目受理页面
    Select Frame    id=InquiryAndProject_ProjectAccept
    等待列表创建完毕
    Transaction End    打开项目受理页面
    切换到已立项子模块
    等待列表创建完毕    打开已立项界面
    输入已立项查询条件    ${流水号}    ${报告编号}    ${项目地址}    ${小区名称}    ${估价目的}    ${报告类型}
    ...    ${业务来源}    ${物业类型}    ${受理人}    ${受理开始时间}    ${受理结束时间}    ${估价委托方}
    ...    ${客户姓名}    ${客户手机}    ${看房联系人电话}    ${资料是否齐全}    ${收费责任人}    ${外业是否完成}
    点击查询按钮
    等待列表创建完毕
    ${表格列数}    Get Matching Xpath Count    .//div[@class="panel" and contains(@style,"display: block")]//*[@class="datagrid-btable"]//tr[@datagrid-row-index]/td[@field="bscode"]/div
    Run Keyword And Continue On Failure    Should Be Equal    ${表格列数}    0
    Comment    清空已立项查询条件
    Unselect Frame

查询项目详情并获取一个pep对象
    [Arguments]    ${流水号}
    打开功能模块    询价立项管理    项目受理
    Select Frame    id=InquiryAndProject_ProjectAccept
    等待列表创建完毕
    切换到已立项子模块
    等待列表创建完毕    打开已立项模块
    输入已立项查询条件    ${流水号}
    点击查询按钮
    等待列表创建完毕    已立项-查询
    点击列表中的查看按钮
    Unselect Frame
    Wait Until Page Contains Element    xpath=//iframe[contains(@id,'${流水号}')]
    Select Frame    xpath=//iframe[contains(@id,'${流水号}')]
    ${pep}    Create Pep Object
    Set Pep Value    ${pep}    流水号    ${流水号}
    将立项界面的信息写入pep对象    ${pep}    1
    Unselect Frame
    关闭功能模块    项目详细-${流水号}
    [Return]    ${pep}

查看线上业务详情并获取一个pep对象
    [Arguments]    ${交易编号}=None    ${项目流水号}=None    ${小区名称}=None    ${物业地址}=None    ${项目来源}=None    ${是否已受理}=None
    ...    ${是否已终止}=None    ${业务类型}=None
    线上业务有效查询    ${交易编号}    ${项目流水号}    ${小区名称}    ${物业地址}    ${项目来源}    ${是否已受理}
    ...    ${是否已终止}    ${业务类型}
    Select Frame    id=InquiryAndProject_ProjectAccept
    Click Element    xpath=.//div[@class="panel" and contains(@style,"display: block")]//tr[@datagrid-row-index="0"]//a[text()="查看"]    #点击查看按钮
    Unselect Frame
    Select Frame    id=View_OnlineBusiness
    ${pep}    Create Pep Object
    将线上业务的信息写入pep对象    ${pep}
    [Return]    ${pep}

线上业务进行立项并分配操作
    ${pep}    查看线上业务详情并获取一个pep对象    业务类型=正式委托    小区名称=链家
    Click Element    xpath=.//span[text()="受理"]/ancestor::a
    Unselect Frame
    Select Frame    id=create_project_
    验证立项界面的值是否正常    ${pep}    跳转来源=2
    ${pep_new}    Create Pep Object
    Set Pep Value    ${pep_new}    市场负责人    ${USER_NAME}
    Set Pep Value    ${pep_new}    收费责任人    ${USER_NAME}
    Set Pep Value    ${pep_new}    内部报单人    ${USER_NAME}
    ${time}    获取当前时间    %Y-%m-%d %H:%M:%S
    Set Pep Value    ${pep_new}    立项备注    这是${time}进行处理的线上业务数据
    填写立项界面的信息    ${pep_new}
    将立项界面的信息写入pep对象    ${pep}    是否清空列表数据=1
    进行任务分配操作    ${pep}
    查询并验证项目详情的值是否正确    ${pep}
    [Return]    ${pep}
